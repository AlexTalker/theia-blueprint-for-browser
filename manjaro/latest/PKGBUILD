# Maintainer: Alex Talker <alextalker at ya.ru>
pkgbase='theia-blueprint-for-browser'
pkgname=('theia-blueprint-for-browser'
         'theia-blueprint-for-browser-extra')
pkgver=1.25.0
pkgrel=1
arch=('x86_64')
url='https://www.theia-ide.org/'
pkgdesc="Cloud IDE Platform blueprint"
license=('EPL2')
depends=('nodejs' 'nss')
makedepends=('yarn' 'node-gyp' 'libsecret')
optdepends=('git: git support' 'libsecret: keytar support')
options=(!strip) #to speed up build

source=(
    "theia-blueprint-for-browser.sh"
    "theia-blueprint-for-browser@.service"
    "package.json"
    "https://raw.githubusercontent.com/eclipse-theia/theia/v$pkgver/LICENSE"
)
md5sums=('SKIP'
         'SKIP'
         'SKIP'
         '84e52389f141be88a24bcfdd44c91a19')

build() {
    local FAKEHOME="$srcdir/.nodejs-gyp"
    export YARN_CACHE_FOLDER="$srcdir/.yarn-cache"
    mkdir -p "$FAKEHOME"

    # See https://theia-ide.org/docs/composing_applications
    HOME="$FAKEHOME" yarn
    HOME="$FAKEHOME" yarn theia build
    HOME="$FAKEHOME" yarn theia download:plugins

    unset YARN_CACHE_FOLDER
}

package_theia-blueprint-for-browser() {
    # Create directory
    install -dm 755 "${pkgdir}/opt/${pkgbase}"

    # Source code (symlinks are not dereferenced) and plugins
    cp -r --no-preserve=ownership --preserve=mode \
        src-gen lib node_modules \
        plugins \
        "${pkgdir}/opt/${pkgbase}/"

    # package.json
    install -Dm755 package.json "${pkgdir}/opt/${pkgbase}/"

    # Executable
    install -Dm755 "${pkgbase}.sh" "${pkgdir}/usr/bin/${pkgbase}"

    # License
    install -Dm 644 LICENSE "${pkgdir}/usr/share/licenses/${pkgbase}/LICENSE"
}

package_theia-blueprint-for-browser-extra() {
    # Optional helpful files
    arch=('any')
    pkgdesc='Additional useful files for Cloud IDE Platform blueprint'
    depends=("${pkgbase}")
    optdepends=('systemd: For systemd unit file')

    install -Dm 644 "${pkgbase}@.service" "${pkgdir}/usr/lib/systemd/user/${pkgbase}@.service"
}
